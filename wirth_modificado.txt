D = "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9".
L = "a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z"|"A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z".
SPECIAL = "@"|"#"|"$"|"%"|"""|"'"|"&"|"*"|"("|")"|"["|"]"|"<"|">"|"="|"-"|"^"|"~""|"\"|"/"|","|"."|":"|";".
ESPACO = "\n" | "\t" | " ".
ANY = L | D | SPECIAL | ESPACO.
ID = (L | "_"){L | D | "_"}.
TIPO = "int"|"float"|"string"|"bool".
STRING = """{ANY}""" | "'"{ANY}"'".
INT = D{D}.
FLOAT = INT"."INT.
BOOL = "true" | "false".
COMPARADOR = "<"|">"|"<="|">="|"=="|"!=".
	
ELEM_VETOR = ID"["INT"]".
ELEM_MATRIZ = ID"["INT"]" "["INT"]".
PROGRAMA = "begin" COMANDOS "end".
COMANDOS = COMANDO";"{ COMANDO";"}.
COMANDO = ATRIBUICAO | DECLARACAO | CONDICIONAL | ITERACAO | SELECAO | ENTRADA | SAIDA.
ATRIBUICAO = VARIAVEL "=" VALOR.
DECLARACAO = DECLARACAO_VARIAVEL | ("function" TIPO ID"("[(TIPO ID){","(TIPO ID)}]")" "do" COMANDOS [("return" ";" | "return" VALOR ";")] "endfunction" ";").
DECLARACAO_VARIAVEL = TIPO VARIAVEL{","VARIAVEL}.
CONDICIONAL = "if" "(" CONDICAO ")" "do" COMANDOS ELSE "endif".
ELSE = {"elsif" "(" CONDICAO ")" "do" COMANDOS }["else" "do" COMANDOS].
CONDICAO = BOOL | EXPRESSAO COMPARADOR EXPRESSAO | VALOR_GENERICO | EXPRESSAO.
ITERACAO = "while" "(" CONDICAO ")"  "do" COMANDOS "endwhile".
SELECAO = "when" "(" VALOR ")" CASO{CASO} "endwhen".
CASO = "is" "(" VALOR ")" "do" COMANDOS "continue" ";".
ENTRADA = "scan" "(" VARIAVEL{","VARIAVEL} ")".
SAIDA = "print" "(" VALOR{","VALOR} ")".

EXPRESSAO = TERMO { ("+"|"-"|"or") TERMO }.
TERMO = FATOR { ("*"|"/"|"and") FATOR }.
FATOR = "("EXPRESSAO")" | INT | FLOAT | BOOL | VALOR_GENERICO.

ACESSO_STRUCT = ID"."ID.
CHAMADA_FUNC = ID"("[{VALOR","}VALOR]")".
VALOR_GENERICO = ID | ELEM_VETOR | ELEM_MATRIZ | CHAMADA_FUNC | ACESSO_STRUCT .
VALOR = INT | FLOAT | STRING | BOOL | EXPRESSAO | VALOR_GENERICO.
VARIAVEL = ID | ELEM_VETOR | ELEM_MATRIZ.
